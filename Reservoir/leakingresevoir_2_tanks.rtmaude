(tmod RESERVOIR is
	protecting POSRAT-TIME-DOMAIN . *** Dense time domain
	ops w v1 v2 r1 r2 : -> NNegRat .
	
	sort Hose .
	ops left right : -> Hose [ctor] .
	op _`,_`,_ : Hose NNegRat NNegRat -> System [ctor] .

	vars x1 x2 : NNegRat .
	*** Move hose instantaneously 
	crl [moveright] : left,x1,x2 => right,x1,x2 
		if x2 <= r2 . 
	crl [moveleft] : right,x1,x2 => left,x1,x2 
		if x1 <= r1 .
	
	*** Time Tick rule
	var R : Time .
	crl [tick-right] :
		{right, x1, x2} => {right, x1 - (v1 * R), 
		x2 + ((w - v2) * R)}
		in time R 
	if x1 > r1 [nonexec] .
			
	crl [tick-left] :
		{left, x1, x2} => {left, x1 + ((w - v1) * R), 
		x2 - (v2 * R)}
		in time R
	if x2 > r2 [nonexec] .
endtm)

(tmod TEST is inc RESERVOIR .
	eq w = 10 .
	eq v1 = 5 . eq v2 = 5 .
	eq r1 = 15 . eq r2 = 15 .
endtm)